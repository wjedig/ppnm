set terminal svg background "white"
set key left
set xlabel "time (days)"
set ylabel "Activity of ThX (relative units)"
set tics out
set title "Radioactive decay of ThX"
set output "Decay.svg"

load "out.decayfit.par"
F_low(x) = exp(a-a_err)*exp((-b-b_err)*x)
F(x) = exp(a)*exp(-b*x)
F_high(x) = exp(a+a_err)*exp((-b+b_err)*x)

plot [0:16][0:150] "decay.txt" u 1:2:3 w yerr title "experiment",\
	F_low(x) with lines title "min fit",\
	F(x) with lines title "fit",\
	F_high(x) with lines title "max fit"
	
